#
#  W A R N I N G
#  -------------
#
# This file is not part of the Qt API.  It exists purely as an
# implementation detail.  It may change from version to version
# without notice, or even be removed.
#
# We mean it.
#

TEMPLATE = app

load(qt_build_paths)
DESTDIR = $$MODULE_BASE_OUTDIR/bin

isEmpty(QMAKE_INFO_PLIST): CONFIG -= app_bundle

# This decreases the binary size for tools if statically linked
QMAKE_LFLAGS += $$QMAKE_LFLAGS_GCSECTIONS

host_build: QT -= gui   # no host tool will ever use gui
host_build:force_bootstrap {
    !build_pass: CONFIG += release
    contains(QT, core(-private)?|xml) {
        QT -= core core-private xml
        QT += bootstrap-private
    }
    target.path = $$[QT_HOST_BINS]
} else {
    !build_pass:contains(QT_CONFIG, debug_and_release):contains(QT_CONFIG, build_all): CONFIG += release
    target.path = $$[QT_INSTALL_BINS]
}
INSTALLS += target

load(qt_targets)
load(qt_common)
