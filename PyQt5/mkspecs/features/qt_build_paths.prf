#
#  W A R N I N G
#  -------------
#
# This file is not part of the Qt API.  It exists purely as an
# implementation detail.  It may change from version to version
# without notice, or even be removed.
#
# We mean it.
#

# Find the module's source root dir.
isEmpty(_QMAKE_CONF_): error("Project has no top-level .qmake.conf file.")
MODULE_BASE_INDIR = $$dirname(_QMAKE_CONF_)
MODULE_BASE_OUTDIR = $$shadowed($$MODULE_BASE_INDIR)
!isEmpty(MODULE_BASE_DIR): MODULE_SYNCQT_DIR = $$MODULE_BASE_DIR    # compat for webkit
isEmpty(MODULE_SYNCQT_DIR): MODULE_SYNCQT_DIR = $$MODULE_BASE_INDIR
isEmpty(MODULE_QMAKE_OUTDIR): MODULE_QMAKE_OUTDIR = $$MODULE_BASE_OUTDIR

exists($$MODULE_BASE_INDIR/.git): \
    CONFIG += git_build

!prefix_build {
    QTDIR = $$[QT_HOST_PREFIX]
    # Permit modules to enforce being built outside QTDIR ...
    !force_independent: MODULE_BASE_OUTDIR = $$QTDIR
    # ... though this sort of breaks the idea.
    MODULE_QMAKE_OUTDIR = $$QTDIR
}
