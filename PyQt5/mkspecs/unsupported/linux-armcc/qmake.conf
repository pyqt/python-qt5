#
# qmake configuration for linux-armcc
#

MAKEFILE_GENERATOR      = UNIX
CONFIG                 += incremental armcc_linker
QMAKE_INCREMENTAL_STYLE = sublib

include(../../common/linux.conf)
include(../../common/armcc.conf)
load(qt_config)

# use armcc for linking since armlink doesn't understand the arm_linux_config_file option
QMAKE_LINK              = armcc
QMAKE_LINK_SHLIB        = armcc
QMAKE_LINK_C            = armcc
QMAKE_LINK_C_SHLIB      = armcc

QMAKE_LFLAGS_SHLIB     += --apcs=/fpic --shared
QMAKE_LFLAGS_PLUGIN     = $$QMAKE_LFLAGS_SHLIB

QMAKE_LIBS             += libstdc++.so librt.so

CONFIG                 -= rvct_linker

QMAKE_CFLAGS           += --gnu --arm_linux --dllimport_runtime --thumb --cpu Cortex-A9 --arm_linux_config_file="$(HOME)/qt_rvct_config" --arm-linux-paths --apcs=/interwork --visibility_inlines_hidden --diag_suppress 1300,2523
QMAKE_CXXFLAGS         += $$QMAKE_CFLAGS --cpp

QMAKE_LFLAGS           += --diag_suppress 6331,6780,6439 --arm_linux_config_file="$(HOME)/qt_rvct_config" --arm-linux-paths
